let
    type int_array = array of int
    function print_array (arr: int_array) =
        (
            print("[");
            for i:= 0 to array_length(arr) - 1 do print_arr_ele(arr[i]);
            print("");
            print("]")
        )

    function create_array(): int_array = 
        let
            var size := 8 
            var arr := int_array [size] of 1
        in
            for i := 0 to size - 1 do arr[i] := random(50);
            arr
        end
     function quick_sort(arr: int_array) =
        let
            function partition(first: int, last: int): int =
                let
                    var wall_index := first
                    var pivot := arr[last]
                    var final_wall_value := arr[wall_index]
                in
                    for i := first to last -1 do 
                        let
                            var current := arr[i]
                            var wall_item := arr[wall_index]
                        in
                            if current < pivot
                            then (
                                arr[wall_index] := current;
                                arr[i] := wall_item;
                                wall_index := wall_index + 1
                            )
                        end;
                    final_wall_value := arr[wall_index];
                    arr[wall_index] := pivot;
                    arr[last] := final_wall_value;
                    wall_index
                end

            function sort(first: int, last: int) =
                if first < last
                then
                    let
                        var pivot_index := partition(first, last)
                    in
                        sort (first, pivot_index -1);
                        sort (pivot_index + 1, last)
                    end
        in
           sort(0, array_length(arr) - 1)
        end
    var arr := create_array()

    function create_array_test(): int_array = 
        let
            var size := 5 
            var arr := int_array [size] of 1
        in
            for i := 0 to size - 1 do arr[i] := size-i-1;
            arr
        end
    function assert_int(actual: int, expected: int) = 
		assert_equal_int("quick_sort.tig", actual, expected)
    var arr_test := create_array_test()
in
    print("Before sorting");
    print_array(arr);
    print("==============");
    quick_sort(arr);
    print("After sorting");
    print_array(arr);

    /* Testing start here */
    assert_int(arr_test[0], 4);
    assert_int(arr_test[1], 3);
    assert_int(arr_test[2], 2);
    assert_int(arr_test[3], 1);
    assert_int(arr_test[4], 0);

    quick_sort(arr_test);

    assert_int(arr_test[0], 0);
    assert_int(arr_test[1], 1);
    assert_int(arr_test[2], 2);
    assert_int(arr_test[3], 3);
    assert_int(arr_test[4], 4)
end